/*
 * Copyright (c) 2023 - 2025 Rachit
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#version 460

#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_buffer_reference2    : enable
#extension GL_EXT_scalar_block_layout  : enable

#include "Constants.glsl"
#include "Culling/Frustum.h"

layout(local_size_x = 64) in;

bool IsVisible(Mesh mesh);

void main()
{
    uint index = gl_GlobalInvocationID.x;

    if (index >= Constants.DrawCalls.count)
    {
        return;
    }

    if (index == 0)
    {
        Constants.CulledOpaqueDrawCalls.count                 = 0;
        Constants.CulledOpaqueDoubleSidedDrawCalls.count      = 0;
        Constants.CulledAlphaMaskedDrawCalls.count            = 0;
        Constants.CulledAlphaMaskedDoubleSidedDrawCalls.count = 0;
    }

    memoryBarrierBuffer();
    barrier();

    Mesh mesh = Constants.Meshes.meshes[index];

    if (!IsVisible(mesh))
    {
        return;
    }

    Material material = mesh.material;

    bool isDoubleSided = Material_IsDoubleSided(material);
    bool isAlphaMasked = Material_IsAlphaMasked(material);

    if (isDoubleSided && !isAlphaMasked)
    {
        uint drawIndex = atomicAdd(Constants.CulledOpaqueDoubleSidedDrawCalls.count, 1);

        Constants.CulledOpaqueDoubleSidedDrawCalls.drawCalls[drawIndex] = Constants.DrawCalls.drawCalls[index];
        Constants.CulledOpaqueDoubleSidedMeshIndices.indices[drawIndex] = index;
    }
    else if (isAlphaMasked && !isDoubleSided)
    {
        uint drawIndex = atomicAdd(Constants.CulledAlphaMaskedDrawCalls.count, 1);

        Constants.CulledAlphaMaskedDrawCalls.drawCalls[drawIndex] = Constants.DrawCalls.drawCalls[index];
        Constants.CulledAlphaMaskedMeshIndices.indices[drawIndex] = index;
    }
    else if (isDoubleSided && isAlphaMasked)
    {
        uint drawIndex = atomicAdd(Constants.CulledAlphaMaskedDoubleSidedDrawCalls.count, 1);

        Constants.CulledAlphaMaskedDoubleSidedDrawCalls.drawCalls[drawIndex] = Constants.DrawCalls.drawCalls[index];
        Constants.CulledAlphaMaskedDoubleSidedMeshIndices.indices[drawIndex] = index;
    }
    else
    {
        uint drawIndex = atomicAdd(Constants.CulledOpaqueDrawCalls.count, 1);

        Constants.CulledOpaqueDrawCalls.drawCalls[drawIndex] = Constants.DrawCalls.drawCalls[index];
        Constants.CulledOpaqueMeshIndices.indices[drawIndex] = index;
    }
}

bool IsVisible(Mesh mesh)
{
    AABB    aabb    = AABB_Transform(mesh.aabb, mesh.transform);
    vec3[8] corners = AABB_GetCorners(aabb);

    for (uint i = 0; i < 6; ++i)
    {
        Plane plane = Constants.Frustum.planes[i];

        uint outside = 0;

        for (uint j = 0; j < 8; ++j)
        {
            if ((dot(plane.normal, corners[j]) + plane.distance) < 0.0f)
            {
                ++outside;
            }
        }

        if (outside == 8)
        {
            return false;
        }
    }

    return true;
}